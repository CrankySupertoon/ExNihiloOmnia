buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.40'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'


version = "1.0"
group = "com.jozufozu.exnihiloomnia"
archivesBaseName = "exnihiloomnia"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = compileKotlin.kotlinOptions.jvmTarget = compileTestKotlin.kotlinOptions.jvmTarget = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    version = project.mc_version + "-" + project.forge_version
    runDir = "run"

    mappings = "snapshot_20170624"
}

repositories {
    jcenter()

    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }

    maven { // TOP
        name = 'tterrag maven'
        url = "http://maven.tterrag.com/"
    }

    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }

    maven { //Albedo
        url = 'https://repo.elytradev.com/'
    }

    maven {
        url = "http://maven.shadowfacts.net/"
    }
}

dependencies {
    compile group: "net.shadowfacts", name: "Forgelin", version: "1.8.3"

    deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"

    deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-${top_version}"

    //deobfCompile 'elucent:albedo:2.0-SNAPSHOT'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    archiveName = baseName + "_" + project.mc_version + "-" + project.version  + "." + extension

    exclude "**/*.bat"
    exclude "**/*.psd"
    exclude "**/*.exe"
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    archiveName = baseName + "_" + project.mc_version + "-" + project.version  + "-sources." + extension
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    archiveName = baseName + "_" + project.mc_version + "-" + project.version  + "-deobf." + extension
}

// Tell the artifact system about our extra jars
artifacts {
    archives srcJar, deobfJar
}
